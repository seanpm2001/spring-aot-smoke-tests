plugins {
	id "checkstyle"
	id "io.spring.javaformat"
	id "java-gradle-plugin"
}

repositories {
	mavenCentral()
	maven { url "https://repo.spring.io/milestone" }
	maven { url "https://repo.spring.io/snapshot" }
}

gradlePlugin {
	plugins {
		aotSmokeTestPlugin {
			id = "org.springframework.aot.smoke-test"
			implementationClass = "org.springframework.aot.gradle.AotSmokeTestPlugin"
		}
	}
}

new File(rootDir.parentFile.parentFile.parentFile, "gradle.properties").withInputStream {
	def properties = new Properties()
	properties.load(it)
	properties.each { key, value ->
		if (key.endsWith("Version")) {
			project.ext.set(key, value)
		}
	}
}

dependencies {
	checkstyle("io.spring.javaformat:spring-javaformat-checkstyle:$javaFormatVersion")

	compileOnly("io.spring.javaformat:spring-javaformat-gradle-plugin:$javaFormatVersion")
	compileOnly("org.graalvm.buildtools:native-gradle-plugin:$nbtVersion")
	compileOnly("org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion")
	compileOnly("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion")

	implementation("com.avast.gradle:gradle-docker-compose-plugin:0.16.8")

	testImplementation("org.junit.jupiter:junit-jupiter:5.10.1")
	testImplementation("org.mockito:mockito-core:5.7.0")
}

sourceCompatibility = "17"
targetCompatibility = "17"
